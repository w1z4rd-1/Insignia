plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT'
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = "Insignia"
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}
	maven {
		name = 'Shedaniel Maven'
		url = 'https://maven.shedaniel.me/'
	}
	mavenCentral()
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	
	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	
	// Cloth Config API for nice config GUI
	modImplementation "me.shedaniel.cloth:cloth-config-fabric:15.0.140"
	
	// Include Fabric API as a Jar-in-Jar dependency
	include "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	include "me.shedaniel.cloth:cloth-config-fabric:15.0.140"

	testImplementation "org.junit.jupiter:junit-jupiter:5.10.2"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

loom {
	runs {
		// This adds a new gradle task that runs the datagen API: "gradlew runDatagen"
		datagen {
			inherit client
			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
			vmArg "-Dfabric-api.datagen.modid=insignia"
 
			runDir "build/datagen"
		}
	}
}

// Add the generated resources to the main source set
sourceSets {
	main {
		resources {
			srcDirs += [
					'src/main/generated'
			]
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

tasks.withType(Test).configureEach {
	useJUnitPlatform()
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_Insignia"}
	}
}

import java.nio.file.Files
import java.nio.file.StandardCopyOption
import java.nio.file.AtomicMoveNotSupportedException
import java.util.jar.JarFile

// Add a task to copy the mod to the PrismLauncher instance with a staged, validated write.
tasks.register("copyToPrismLauncher") {
	dependsOn remapJar
	doLast {
		File sourceJar = remapJar.archiveFile.get().asFile
		File modsDir = file("C:/Users/Terac/AppData/Roaming/PrismLauncher/instances/Simply Optimized 1.21.3-1.0/.minecraft/mods")
		File targetJar = new File(modsDir, "Insignia.jar")
		File stagedJar = new File(modsDir, "Insignia.jar.part")
		File backupJar = new File(modsDir, "Insignia.jar.bak")

		modsDir.mkdirs()

		// Always copy to a temporary file first so a failed copy never corrupts the live mod jar.
		Files.copy(sourceJar.toPath(), stagedJar.toPath(), StandardCopyOption.REPLACE_EXISTING)

		// Validate staged jar before swapping into mods directory.
		new JarFile(stagedJar).close()

		if (targetJar.exists()) {
			Files.copy(targetJar.toPath(), backupJar.toPath(), StandardCopyOption.REPLACE_EXISTING)
		}

		try {
			try {
				Files.move(stagedJar.toPath(), targetJar.toPath(), StandardCopyOption.REPLACE_EXISTING, StandardCopyOption.ATOMIC_MOVE)
			} catch (AtomicMoveNotSupportedException ignored) {
				Files.move(stagedJar.toPath(), targetJar.toPath(), StandardCopyOption.REPLACE_EXISTING)
			}
		} catch (Exception moveError) {
			logger.warn("copyToPrismLauncher skipped: could not replace Insignia.jar (likely file lock while game is running): ${moveError.message}")
			return
		}

		// Validate final jar; restore backup if anything is off.
		try {
			new JarFile(targetJar).close()
		} catch (Exception e) {
			if (backupJar.exists()) {
				Files.move(backupJar.toPath(), targetJar.toPath(), StandardCopyOption.REPLACE_EXISTING)
			}
			throw new GradleException("Copied JAR failed validation; restored previous Insignia.jar", e)
		}

		if (backupJar.exists()) {
			backupJar.delete()
		}
	}
}

// Make the build task depend on copyToPrismLauncher so it automatically copies the JAR
build.finalizedBy(copyToPrismLauncher)

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
